buildscript {
    ext.kotlin_version = '1.5.10'
    ext.spring_boot_version = '2.5.1'
    ext.jackson_version = '2.12.3'
    ext.lombok_version = '1.18.20'
    ext.mockito_version = '3.10.0'
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version")
        classpath("org.owasp:dependency-check-gradle:6.2.0")
        classpath("gradle.plugin.org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.12.0")
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.palantir.git-version:com.palantir.git-version.gradle.plugin:0.12.3"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.owasp.dependencycheck'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'com.palantir.git-version'

jacoco {
    toolVersion = "0.8.7+"
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
    }
}
[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

//runtime("org.springframework.boot:spring-boot-properties-migrator")
jar {
    archivesBaseName = 'boat'
    archiveVersion = getTagVersion()
}

repositories {
    mavenCentral()
}

sourceCompatibility = 11
targetCompatibility = 11

dependencies {
    // tag::jetty[]
    implementation("org.springframework.boot:spring-boot-starter-web:$spring_boot_version") {
        exclude module: "spring-boot-starter-tomcat"
    }
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-jetty', version: "$spring_boot_version"
    // end::jetty[]
    // tag::actuator[]
    implementation("org.springframework.boot:spring-boot-starter-actuator:$spring_boot_version")
    // end::actuator[]
    implementation("org.jsoup:jsoup:1.13.1")

    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.7'

    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: "$jackson_version"
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: "$jackson_version"
    implementation group: 'org.springframework.boot', name: 'spring-boot-properties-migrator', version: "$spring_boot_version"
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-cache', version: "$spring_boot_version"
    implementation group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: "3.0.2"
    implementation group: 'commons-io', name: 'commons-io', version: '2.10.0'

    implementation "org.projectlombok:lombok:$lombok_version"
    annotationProcessor "org.projectlombok:lombok:$lombok_version"

    testImplementation "org.projectlombok:lombok:$lombok_version"
    testAnnotationProcessor "org.projectlombok:lombok:$lombok_version"

    //implementation "io.reactivex.rxjava3:rxjava:3.0.12"

    runtimeOnly("org.springframework.boot:spring-boot-properties-migrator")

    testImplementation group: 'org.mockito', name: 'mockito-core', version: mockito_version
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: mockito_version
    testImplementation "io.mockk:mockk:1.11.0"
    testImplementation('org.junit.jupiter:junit-jupiter:5.7.2')
    testImplementation("org.springframework.boot:spring-boot-starter-test:$spring_boot_version")
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

}

def getCheckedOutGitCommitHash() {
    def gitFolder = "$projectDir/.git/"
    def takeFromHash = 12
    /*
     * '.git/HEAD' contains either
     *      in case of detached head: the currently checked out commit hash
     *      otherwise: a reference to a file containing the current commit hash
     */
    def head = new File(gitFolder + "HEAD").text.split(":") // .git/HEAD
    def isCommit = head.length == 1 // e5a7c79edabbf7dd39888442df081b1c9d8e88fd
    // def isRef = head.length > 1     // ref: refs/heads/master

    if (isCommit) return head[0].trim().take(takeFromHash) // e5a7c79edabb

    def refHead = new File(gitFolder + head[1].trim()) // .git/refs/heads/master
    refHead.text.trim().take takeFromHash
}

def getTagVersion() {
    ['sh', '-c', 'git describe --tags --always --first-parent'].execute().text.trim()
}

task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = jar.archiveVersion.get() + "-" + getCheckedOutGitCommitHash() + "-" + new Date().format('yyyy-MM-dd HH:mm:ss')
            println('Version:'+p['version'])
            p.store w, null
        }
    }
}

classes {
    dependsOn createProperties
}

test {
    useJUnitPlatform()
    testLogging {
        events "standardOut", "started", "passed", "skipped", "failed"
        showStandardStreams = true
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
