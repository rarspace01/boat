buildscript {
    ext.spring_version = '2.2.4.RELEASE'
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$spring_version")
        classpath("org.owasp:dependency-check-gradle:5.3.0")
        classpath("org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.3")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.owasp.dependencycheck'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'

jacoco {
    toolVersion = "0.8.5+"
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
    }
}
[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

//runtime("org.springframework.boot:spring-boot-properties-migrator")
jar {
    archivesBaseName = 'pirateboat'
    archiveVersion = '0.6.14'
}

repositories {
    mavenCentral()
}

sourceCompatibility = 13
targetCompatibility = 13

dependencies {
    // tag::jetty[]
    implementation("org.springframework.boot:spring-boot-starter-web:$spring_version") {
        exclude module: "spring-boot-starter-tomcat"
    }
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-jetty', version: "$spring_version"
    // end::jetty[]
    // tag::actuator[]
    implementation("org.springframework.boot:spring-boot-starter-actuator:$spring_version")
    // end::actuator[]
    implementation("org.jsoup:jsoup:1.12.2")

    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.5'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.10.2'
    implementation group: 'org.springframework.boot', name: 'spring-boot-properties-migrator', version: "$spring_version"
    implementation group: 'commons-io', name: 'commons-io', version: '2.6'

    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'

    testCompileOnly 'org.projectlombok:lombok:1.18.12'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'

    runtimeOnly("org.springframework.boot:spring-boot-properties-migrator")
    testCompileOnly group: 'org.mockito', name: 'mockito-core', version: '3.2.4'
    testCompileOnly group: 'org.mockito', name: 'mockito-junit-jupiter', version: '3.2.4'
    testImplementation('org.junit.jupiter:junit-jupiter:5.6.0')
    testImplementation("org.springframework.boot:spring-boot-starter-test:$spring_version")

}

def getCheckedOutGitCommitHash() {
    def gitFolder = "$projectDir/.git/"
    def takeFromHash = 12
    /*
     * '.git/HEAD' contains either
     *      in case of detached head: the currently checked out commit hash
     *      otherwise: a reference to a file containing the current commit hash
     */
    def head = new File(gitFolder + "HEAD").text.split(":") // .git/HEAD
    def isCommit = head.length == 1 // e5a7c79edabbf7dd39888442df081b1c9d8e88fd
    // def isRef = head.length > 1     // ref: refs/heads/master

    if(isCommit) return head[0].trim().take(takeFromHash) // e5a7c79edabb

    def refHead = new File(gitFolder + head[1].trim()) // .git/refs/heads/master
    refHead.text.trim().take takeFromHash
}

task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = jar.archiveVersion.get() + "-" + getCheckedOutGitCommitHash() + "-" + new Date().format('yyyy-MM-dd HH:mm:ss')
            p.store w, null
        }
    }
}

classes {
    dependsOn createProperties
}

test {
    useJUnitPlatform()
    testLogging {
        events "standardOut", "started", "passed", "skipped", "failed"
        showStandardStreams = true
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}